plugins {
  id 'fabric-loom' version '1.7-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

var javaVersion = project.java_version.toInteger()

base {
  archivesName = project.archives_base_name
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
  inputs.property "version", project.version

  filesMatching(["fabric.mod.json", "lesser-x.mixins.json"]) {
    expand(
      "version": project.version,
      "loader_version": project.loader_version,
      "java_version": project.java_version,
      "minecraft_version": project.minecraft_version
    )
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.release = javaVersion
  it.options.encoding = "UTF-8"
}

java {
  withSourcesJar()

  sourceCompatibility = JavaVersion.toVersion(javaVersion)
  targetCompatibility = sourceCompatibility
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.base.archivesName.get()}" }
  }
}
